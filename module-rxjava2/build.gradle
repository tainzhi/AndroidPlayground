import com.tainzhi.android.buildsrc.Libs

// ARouter switch
if (isModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

// ARouter
kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}
android {
    compileSdkVersion Libs.Version.compileSdkVersion
    buildToolsVersion Libs.Version.buildToolsVersion
    defaultConfig {
        if (!isModule.toBoolean()) {
            applicationId "com.tanzhi.sample.rxjava2"
        }
        minSdkVersion Libs.Version.minSdkVersion
        targetSdkVersion Libs.Version.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/buildModule/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.constraintlayout
    implementation Libs.Google.material
    implementation Libs.RxJava.rxJava
    implementation Libs.RxJava.rxAndroid

    implementation(Libs.ARouter.api)
    kapt(Libs.ARouter.compiler)

    implementation(Libs.baseRecyclerViewAdapterHelper)
    implementation(Libs.Retrofit.retrofit)
    implementation(Libs.Retrofit.gsonConverter)
    implementation(Libs.Retrofit.retrofit_rxjava_adapter)
    implementation(Libs.logger)
}
